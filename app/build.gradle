import java.util.regex.Matcher
import java.util.regex.Pattern

apply plugin: 'com.android.application'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'

android {
    namespace 'ai.elimu.maneno'
    compileSdk 36

    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 36
        versionCode 2000050
        versionName "2.0.50"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [contentProviderApplicationId: "ai.elimu.content_provider.debug"]
            buildConfigField("String", "CONTENT_PROVIDER_APPLICATION_ID", '"ai.elimu.content_provider.debug"')
            buildConfigField("String", "ANALYTICS_APPLICATION_ID", '"ai.elimu.analytics.debug"')
        }
        release {
            minifyEnabled false
            manifestPlaceholders = [contentProviderApplicationId: "ai.elimu.content_provider"]
            buildConfigField("String", "CONTENT_PROVIDER_APPLICATION_ID", '"ai.elimu.content_provider"')
            buildConfigField("String", "ANALYTICS_APPLICATION_ID", '"ai.elimu.analytics"')
        }
    }

    buildFeatures {
        buildConfig true
    }

    lintOptions {
        abortOnError false
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation libs.androidx.espresso
    androidTestImplementation libs.androidx.junit
    testImplementation libs.junit
    implementation libs.androidx.app.compat
    implementation libs.androidx.cardview
    implementation libs.androidx.core.ktx
    implementation libs.elimu.content.provider
    implementation libs.hilt.android
    kapt libs.hilt.compiler
}

tasks.named('clean', Delete) {
    delete rootProject.layout.buildDirectory.asFile
}

tasks.register('removeSnapshot') {
    doLast {
        println("removeSnapshot")

        def file = file("build.gradle")
        def originalFileContent = file.getText()

        Pattern pattern = Pattern.compile("versionName \"\\d+\\.\\d+\\.\\d+-SNAPSHOT\"")
        Matcher matcher = pattern.matcher(originalFileContent)
        matcher.find()
        println("match: ${matcher.group()}")

        def newVersionName = matcher.group().replace("-SNAPSHOT", "")
        println("newVersionName: ${newVersionName}")

        def newFileContent = originalFileContent.replaceFirst("versionName \"\\d+\\.\\d+\\.\\d+-SNAPSHOT\"", newVersionName)
        file.write(newFileContent)
    }
}
tasks.register('getVersionName') {
    doLast {
        println android.defaultConfig.versionName
    }
}
tasks.register('bumpVersion') {
    doLast {
        println("bumpVersion")

        def currentVersionCode = android.defaultConfig.versionCode
        println("currentVersionCode: ${currentVersionCode}")

        def newVersionCode = currentVersionCode + 1
        println("newVersionCode: ${newVersionCode}")

        def newVersionName = newVersionCode.toString().substring(0, 1).toInteger() + "." + newVersionCode.toString().substring(1, 4).toInteger() + "." + newVersionCode.toString().substring(4, 7).toInteger()
        println("newVersionName: ${newVersionName}")

        def file = file("build.gradle")
        def originalFileContent = file.getText()
        def newFileContent = originalFileContent.replaceFirst("versionCode \\d+", "versionCode ${newVersionCode}")
        newFileContent = newFileContent.replaceFirst("versionName \"\\d+\\.\\d+\\.\\d+\"", "versionName \"${newVersionName}\"")
        file.write(newFileContent)
    }
}
tasks.register('addSnapshot') {
    doLast {
        println("addSnapshot")

        def file = file("build.gradle")
        def originalFileContent = file.getText()

        Pattern pattern = Pattern.compile("versionName \"\\d+\\.\\d+\\.\\d+\"")
        Matcher matcher = pattern.matcher(originalFileContent)
        matcher.find()
        println("match: ${matcher.group()}")

        def newVersionName = "${matcher.group().substring(12, matcher.group().length() - 1)}-SNAPSHOT\""
        println("newVersionName: ${newVersionName}")

        def newFileContent = originalFileContent.replaceFirst("versionName \"\\d+\\.\\d+\\.\\d+\"", "versionName ${newVersionName}")
        file.write(newFileContent)
    }
}

